buildscript{
    dependencies{
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.3'
    }
}

plugins {
    id "java"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "net.ltgt.apt-idea" version "0.21"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id("io.micronaut.application") version "1.5.3"
    id "idea"
    id "groovy"
    id "jacoco"
}


targetCompatibility = "11"
sourceCompatibility = "11"
archivesBaseName = "check-ins-server"

version "0.4"
group "com.objectcomputing.checkins"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
    publicResources
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.objectcomputing.checkins.*")
    }
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor")
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.security:micronaut-security-annotations")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut.cache:micronaut-cache-ehcache")
    implementation("io.micronaut.data:micronaut-data-jdbc")
    implementation("io.micronaut.flyway:micronaut-flyway")
    implementation("io.micronaut.gcp:micronaut-gcp-pubsub")
    implementation("io.micronaut.security:micronaut-security")
    implementation("io.micronaut.security:micronaut-security-jwt")
    implementation("io.micronaut.security:micronaut-security-oauth2")
    implementation("io.micronaut.sql:micronaut-jdbc-hikari")
    implementation("io.micronaut.views:micronaut-views-thymeleaf")
    implementation("io.swagger.core.v3:swagger-annotations")
    implementation("javax.annotation:javax.annotation-api")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.postgresql:postgresql")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:postgresql")
    testImplementation("org.testcontainers:testcontainers")
    implementation("io.micronaut:micronaut-validation")

    compile "commons-io:commons-io:2.8.0"
    compile 'org.apache.commons:commons-csv:1.8'

    implementation "javax.annotation:javax.annotation-api"
    implementation "com.sun.mail:javax.mail:1.6.2"
    implementation "com.mailjet:mailjet-client:4.2.1"
    implementation "com.google.api-client:google-api-client:1.30.9"
    implementation "com.google.oauth-client:google-oauth-client-jetty:1.28.0"
    implementation "com.google.apis:google-api-services-drive:v3-rev20200326-1.30.9"
    implementation "com.google.apis:google-api-services-gmail:v1-rev20200203-1.30.9"
    implementation "com.google.apis:google-api-services-admin-directory:directory_v1-rev118-1.25.0"
    implementation "com.google.apis:google-api-services-chat:v1-rev20200617-1.30.9"
    implementation "com.google.cloud:google-cloud-pubsub:1.108.7"
    implementation "jakarta.persistence:jakarta.persistence-api:2.2.2"
    implementation 'com.google.cloud.sql:postgres-socket-factory:1.0.15'
    runtimeOnly "org.fusesource.jansi:jansi:1.18"

    publicResources project(':web-ui')
}

test.classpath += configurations.developmentOnly

mainClassName = "com.objectcomputing.checkins.Application"
// use JUnit 5 platform
test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
    options.fork = true
    options.forkOptions.jvmArgs << '-Dmicronaut.openapi.views.spec=swagger-ui.enabled=true,swagger-ui.theme=flattop' << '-Dmicronaut.openapi.property.naming.strategy=LOWER_CAMEL_CASE'

    String envStr = System.getenv('MICRONAUT_ENVIRONMENTS')
    List<String> envs = envStr != null ? Arrays.asList(envStr.split(',')) : List.of() as List<String>
    if(!envs.disjoint(List.of("test", "local"))) {
        options.forkOptions.jvmArgs << '-Dmicronaut.openapi.additional.files=src/main/resources/swagger-local'
    } else {
        options.forkOptions.jvmArgs << '-Dmicronaut.openapi.additional.files=src/main/resources/swagger'
    }
}

if(System.getenv("SKIP_WEB_UI") == null) {
    processResources {
        into('public') {
            from configurations.publicResources
        }
    }
}

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote', '-Xdebug',"-Xrunjdwp:transport=dt_socket,address=8000,server=y,suspend=n")


// deploying to gcp with gradlew
//buildscript{
//    dependencies{
//        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.3'
//    }
//}

apply plugin: 'com.google.cloud.tools.appengine'

appengine {
    stage{
        artifact = 'build/libs/check-ins-server-0.1.jar'
    }
    deploy {
        stopPreviousVersion = true
        promote = true
        project = 'oci-intern-2019'
    }
}
