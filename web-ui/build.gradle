plugins {
    id "java"
    id "com.github.node-gradle.node" version "7.0.2"
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

node {
    version = '20.11.1' // https://nodejs.org/en/
    yarnVersion = '1.22.5' // https://yarnpkg.com/en/
    download = true
}

yarn_run_coverage {
    group 'verification'
    outputs.dir 'coverage'
}

def yarn = tasks.named("yarn")

def yarnBuildTask = tasks.register("yarnBuild", YarnTask) {
    group 'build'
    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('public'))
    inputs.files(fileTree('src'))
    inputs.file('index.html')
    inputs.file('package.json')
    inputs.file('tsconfig.json')
    inputs.file('vite.config.ts')
    inputs.file('yarn.lock')

    outputs.dir('dist')

    dependsOn yarn
    args = ['run', 'build']
}

sourceSets {
    java {
        main {
            resources {
                // This makes the processResources task automatically depend on the buildWebapp one
                srcDir yarnBuildTask
            }
        }
    }
}

check {
   dependsOn yarn_run_coverage
}

